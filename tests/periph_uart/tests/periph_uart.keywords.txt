*** Settings ***
Library             UartDevice  port=%{PORT}  baudrate=%{BAUD}  timeout=${10}

Resource            api_shell.keywords.txt
Resource            philip.keywords.txt
Resource            riot_base.keywords.txt

*** Keywords ***
UART Flush
    [Documentation]             Remove garbage from UART buffer
    UART write  flush

UART Init and Flush Should Succeed
    [Documentation]             Init UART device and flush buffer
    [Arguments]                 @{args}  &{kwargs}
    API Call Should Succeed     Uart Init   @{args}  &{kwargs}
    UART Flush

UART Mode Change Should Succeed
    [Documentation]             Configure UART mode and flush
    [Arguments]                 @{args}  &{kwargs}
    API Call Should Succeed     Uart Init
    API Call Should Succeed     Uart Mode   @{args}  &{kwargs}
    UART Flush

UART Mode Should Exist
    [Documentation]             Verify DUT supports UART mode configuration
    ${status}   ${value}=       Run Keyword And Ignore Error   API Call Should Succeed   Uart Mode
    Pass Execution If           '${status}'=='FAIL'   Feature is not supported
